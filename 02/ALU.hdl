// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:

   //stage 1 - multiplex between input and second
   //input tied to ground to zero out
   Mux16(a=x,b[0..15]=false,sel=zx,out=zeroX);
   Mux16(a=y,b[0..15]=false,sel=zy,out=zeroY);

   //stage 2 - Negate inputs
   //Use Mux16 to to choose between
   //output from stage one and negaged output
   //from state one
    Not16(in=zeroX,out=negX);
    Not16(in=zeroY,out=negY);
    Mux16(a=zeroX,b=negX,sel=nx,out=notX);
    Mux16(a=zeroY,b=negY,sel=ny,out=notY);

   //stage3 - Send outputs from 
   //second stage to the 16 bit adder 
   // and the And16 and multiplex between output
   Add16(a=notX,b=notY,out=sum);
   And16(a=notX,b=notY,out=bitAnd);

   Mux16(a=bitAnd,b=sum,sel=f,out=sumAnd);

   //stage4 -- negate using XOR 

   // trick similar to stage 2
   Xor16x1(a=sumAnd,b=no,out[0..7]=lsb,out[8..15]=msb,out[15]=ng,out=out);

  
   //set flags

   //use multiway ors to find out of zero
   Or8Way(in=lsb,out=orLSB);
   Or8Way(in=msb,out=orMSB);
   Or(a=orLSB,b=orMSB,out=zor);
   Not(in=zor,out=zr);




}